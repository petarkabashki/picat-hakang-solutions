/*

  Labeled dice and Building blocks problems in Picat.

  Labeled dice
  --------------
  From Jim Orlin "Colored letters, labeled dice: a logic puzzle"
  http://jimorlin.wordpress.com/2009/02/17/colored-letters-labeled-dice-a-logic-puzzle/
  """
  My daughter Jenn bough a puzzle book, and showed me a cute puzzle.  There 
  are 13 words as follows:  BUOY, CAVE, CELT, FLUB, FORK, HEMP, JUDY, 
  JUNK, LIMN, QUIP, SWAG, VISA, WISH.

  There are 24 different letters that appear in the 13 words.  The question 
  is:  can one assign the 24 letters to 4 different cubes so that the 
  four letters of each word appears on different cubes.  (There is one 
  letter from each word on each cube.)  It might be fun for you to try 
  it.  I'll give a small hint at the end of this post. The puzzle was 
  created by Humphrey Dudley.
  """

  Building blocks
  ---------------
  From http://brownbuffalo.sourceforge.net/BuildingBlocksClues.html
  """
  Each of four alphabet blocks has a single letter of the alphabet on each 
  of its six sides. In all, the four blocks contain every letter but 
  Q and Z. By arranging the blocks in various ways, you can spell all of 
  the words listed below. Can you figure out how the letters are arranged 
  on the four blocks?

  BAKE ONYX ECHO OVAL
  GIRD SMUG JUMP TORN
  LUCK VINY LUSH WRAP
  """

  Note: This is a somewhat generalized model for solving both 
        Building blocks and Labeled Dice problems. 


  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.
import util.

main => go.

go =>
   go(labeling_dice),
   go(building_blocks).


go(Problem) =>
   L = findall(Res,solveit(Problem, Res)),
   writef("It was %d solutions.\n", length(L)).


solveit(Problem,Res) =>

   writef("\nProblem %w\n", Problem),
   problem(Problem, NumCubes, NumSides, Letters, Words),


   % Convert the letters to integers so we can use ic

   % create the integer array
   LettersInt = [I : I in 1..Letters.length],
   NumWords = length(Words),
   WordsIC = new_list(NumWords),
   convert_words(Words,WordsIC,Letters,LettersInt),
  
   CubeLen = NumCubes * NumSides,
   Cube = new_list(CubeLen),
   Cube :: 1..NumCubes,

   % each letters in a word must be on a different die
   foreach(I in 1..NumWords)
      writeln(i=I),
      % element(I,WordsIC,Word),
      Word = WordsIC[I],
      ThisDie = [D : W in Word, element(W,Cube,D)],
      all_different(ThisDie)
   end,

   % there must be exactly NumSides (6) letters of each die
   foreach(I in 1..NumCubes)
      sum([(R #= I) : J in 1..CubeLen, element(J,Cube,R)]) #= NumSides
   end,

   % simple symmetry breaking: place first letter on cube 1
   Cube[1] #= 1,

   % search
   solve(Cube),

   writeln(cube=Cube),

   % print this solution

   % Letters placed
   Res = [[L,D]  : I in 1..CubeLen, double_index(Cube,Letters, I,D,L)],
   writef("Letters placed: %w\n", Res),

   writef("\nWords:\n"),
   foreach(Word in Words)
      foreach(W in Word)
          double_index(Cube,Letters,_Ix, C,W),
          writef("%w: %w ",W,C)
      end,
      nl
   end,

   % print the cubes
   writef("\nCubes:\n"),
   foreach(C in 1..NumCubes)
       writef("Cube %w: ",C),
       ThisCube = [],
       foreach(J in 1..CubeLen)
          % is this letter on the C'th cube?
          double_index(Cube,Letters, J, Val,L),
          if Val == C then
             ThisCube := ThisCube ++[L]
          end
       end,
       writeln(ThisCube)
   end,
   nl, nl.


%
% Lookup a value given an index and/or some value Val1 or Val2
%
double_index(List1,List2,Ix,Val1,Val2) =>
   % element(Ix, List1, Val1),
   nth(Ix, List1, Val1),
   % element(Ix, List2, Val2).
   nth(Ix, List2, Val2).
   
   

    
% convert the matrix of letters (Words) to
% a matrix of integers.
convert_words(Words,WordsIC,Letters,LettersInt) =>
   WordsIC2 = [],
   foreach(Word in Words)
      ThisWord = [WI : W in Word, double_index(Letters, LettersInt,_Ix,W,WI)],
      WordsIC2 := WordsIC2 ++ [ThisWord]
   end,
   WordsIC = WordsIC2.


%
% Labeling Dice
% 
problem(labeling_dice, N, S, Letters, Words) => 
   % number of cubes
   N = 4,  
   % number of sides of of a cube
   S = 6,  
   % the letters to use
   Letters = [a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,y], 
   % the words to place
   Words = [[b,u,o,y],               
            [c,a,v,e], 
            [c,e,l,t], 
            [f,l,u,b], 
            [f,o,r,k], 
            [h,e,m,p], 
            [j,u,d,y], 
            [j,u,n,k], 
            [l,i,m,n], 
            [q,u,i,p], 
            [s,w,a,g], 
            [v,i,s,a], 
            [w,i,s,h]].


%
% Building Blocks
%   From http://brownbuffalo.sourceforge.net/BuildingBlocksClues.html
%   """
%   Each of four alphabet blocks has a single letter of the alphabet on each 
%   of its six sides. In all, the four blocks contain every letter but 
%   Q and Z. By arranging the blocks in various ways, you can spell all of 
%   the words listed below. Can you figure out how the letters are arranged 
%   on the four blocks?
%
%   BAKE ONYX ECHO OVAL
%   GIRD SMUG JUMP TORN 
%   LUCK VINY LUSH WRAP
%   """
problem(building_blocks, N, S, Letters, Words) =>
   N = 4,
   S = 6,
   Letters = [a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,r,s,t,u,v,w,x,y],
   Words = [[b,a,k,e],
            [o,n,y,x],
            [e,c,h,o],
            [o,v,a,l],
            [g,i,r,d],
            [s,m,u,g],
            [j,u,m,p],
            [t,o,r,n],
            [l,u,c,k],
            [v,i,n,y],
            [l,u,s,h],
            [w,r,a,p]].
