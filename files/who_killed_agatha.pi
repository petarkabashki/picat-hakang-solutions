/*

  Who killed agatha? (The Dreadsbury Mansion Murder Mystery) in Picat.

  This is a standard benchmark for theorem proving.
 
  http://www.lsv.ens-cachan.fr/~goubault/H1.dist/H1.1/Doc/h1003.html
  """ 
  Someone in Dreadsbury Mansion killed Aunt Agatha. 
  Agatha, the butler, and Charles live in Dreadsbury Mansion, and 
  are the only ones to live there. A killer always hates, and is no 
  richer than his victim. Charles hates noone that Agatha hates. Agatha 
  hates everybody except the butler. The butler hates everyone not richer 
  than Aunt Agatha. The butler hates everyone whom Agatha hates. 
  Noone hates everyone. Who killed Agatha? 
  """

  Originally from F. J. Pelletier: 
  Seventy-five problems for testing automatic theorem provers. 
  Journal of Automated Reasoning, 2: 191 216, 1986.
  http://www.sfu.ca/~jeffpell/papers/75ATPproblems86.pdf


  I have blogged about the problem here:
  * "Learning constraint programming - part II: Modeling with the Element constraint"
    http://www.hakank.org/constraint_programming_blog/2009/05/learning_constraint_programmin.html
  * "Learning Constraint Programming IV: Logical constraints: Who killed Agatha? revisited"
  http://www.hakank.org/constraint_programming_blog/2009/05/learning_constraint_programmin_3.html

  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

%
% There are 8 solutions: all states that Agatha killed herself.
%
go => 
    % collect all possible solutions
    L = findall(Killer, who_killed_agatha(Killer)),
    writeln(killer=L).


who_killed_agatha(Killer) =>

    % Setup
    N = 3,

    Agatha = 1,
    Butler = 2,
    Charles = 3,

    Killer :: [Agatha,Butler,Charles],

    %
    % define the Hates and Richer matrices
    %
    Hates = new_array(N,N),
    Hates :: 0..1,

    Richer = new_array(N,N),
    Richer :: 0..1,

    %
    % The constraints
    %

    %
    % Agatha, the butler, and Charles live in Dreadsbury Mansion, and 
    % are the only ones to live there. 
    %

    % * A killer always hates, and is no richer than his victim. 
    
    % Note, the following is not supported in Picat
    %
    %   Hates[Killer,Victim] #= 1,
    %   Richer[Killer, Victim] #= 0,
    %
    % This works, though:
    foreach(I in 1..N)
       Killer #= I #=> Hates[I, Agatha] #= 1,
       Killer #= I #=> Richer[I, Agatha] #= 0

       % Killer #= I #=> (Hates[I, Agatha] #= 1 #/\ Richer[I, Agatha] #= 0)

    end,

    % * Define the concept of richer: no one is richer than him-/herself
    foreach(I in 1..N) Richer[I,I] #= 0 end,

    
    % (contd...) if i is richer than j then j is not richer than i
    foreach(I in 1..N, J in 1..N, I != J)
       Richer[I,J] #= 1 #=> Richer[J,I] #= 0,
       Richer[J,I] #= 0 #=> Richer[I,J] #= 1
    end,


    % * Charles hates no one that Agatha hates. 
    foreach(I in 1..N) Hates[Agatha, I] #= 1 #=> Hates[Charles, I] #= 0 end,

    % * Agatha hates everybody except the butler. 
    Hates[Agatha, Butler]  #= 0,
    Hates[Agatha, Charles] #= 1,
    Hates[Agatha, Agatha]  #= 1,

    % * The butler hates everyone not richer than Aunt Agatha. 
    foreach(I in 1 ..N)
      Richer[I, Agatha] #= 0 #=> Hates[Butler, I] #= 1
    end,

    % * The butler hates everyone whom Agatha hates. 
    foreach(I in 1..N) Hates[Agatha, I] #= 1 #=> Hates[Butler, I] #= 1 end,

    % * No one hates every one.
    foreach(I in 1..N) sum([Hates[I,J] : J in 1..N]) #=< 2 end,

    % * A killer always hates, and is no richer than his victim. 
    % Note: When placed here matrix_element/4 works.
    matrix_element(Hates,Killer,Agatha,1),
    matrix_element(Richer,Killer,Agatha,0),


    % * Who killed Agatha?


    Vars = [Killer] ++ Hates ++ Richer,
    solve(Vars).

    % println(hates),
    % foreach(I in 1..N) println(Hates[I]) end,

    % println(richer),
    % foreach(I in 1..N) println(Richer[I]) end.

    %writeln(victim:Victim),
    %writeln(killer:Killer),
    %writeln(hates:Hates),
    %writeln(richer:Richer), fail.


matrix_element1(X, I, J, Val) =>
  element(I, X, Row),
  element(J, Row, Val).

matrix_element2(X, I, J, Val) =>
  nth(I, X, Row),
  element(J, Row, Val).

matrix_element3(X, I, J, Val) =>
  freeze(I, (nth(I, X, Row),freeze(J,nth(J,Row,Val)))).

matrix_element4(X, I, J, Val) =>
  freeze(I, (element(I, X, Row),freeze(J,element(J,Row,Val)))).

matrix_element5(X, I, J, Val) =>
  nth(I, X, Row),
  nth(J, Row, Val).
