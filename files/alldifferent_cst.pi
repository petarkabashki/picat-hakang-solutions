/*

  Global constraint alldifferent_cst in Picat.

  From Global Constraint Catalog:
  http://www.emn.fr/x-info/sdemasse/gccat/Calldifferent_cst.html
  """
  For all pairs of items (VARIABLES[i], VARIABLES[j]) (i!=j) of the 
  collection VARIABLES enforce 
  VARIABLES[i].var+VARIABLES[i].cst != VARIABLES[j].var+VARIABLES[j].cst.
 
  Example
   (<
      var-5 cst-0,
      var-1 cst-1,
      var-9 cst-0,
      var-3 cst-4
   >
   )
  
  The alldifferent_cst constraint holds since all the expressions 
  5+0=5, 1+1=2, 9+0=9 and 3+4=7 correspond to distinct values.
  """

  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go => 
   N = 4,

   Cst = [0,1,0,4],
   % Cst = [0,0,0,0], % for plain all_different 
   % Cst = new_list(N),
   % Cst :: 0..2,

   X = new_list(N),
   X :: 1..9,

   % X = [5,1,9,3],
   L = findall([X,Cst], 
      (alldifferent_cst(X, Cst),
       solve(X++Cst))),
   Len = length(L),
   foreach([X2,Cst2] in L) 
      Y = [I+J : {I,J} in zip(X2,Cst2)],
      writeln([x=X2,cst=Cst2,y=Y]) 
   end,
   write(len=Len),
   nl.

   

alldifferent_cst(Xs, Cst) =>
   Res = [$(X + C) :  {X,C} in zip(Xs,Cst)],
   all_different(Res).


