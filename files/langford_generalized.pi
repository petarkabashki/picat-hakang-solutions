/*

  Langford's number problem (generalized version) in Picat.

  Langford's number problem (CSP lib problem 24)
  http://www.csplib.org/Problems/prob024/
  """
  Consider two sets of the numbers from 1 to 4. The problem is to arrange the eight numbers 
  in the two sets into a single sequence in which the two 1’s appear one number apart, the two 
  2’s appear two numbers apart, the two 3’s appear three numbers apart, and the two 4’s appear four numbers apart.
 
  The problem generalizes to the L(k,n) problem, which is to arrange k sets of numbers 1 to n, 
  so that each appearance of the number m is m numbers on from the last. For example, the 
  L(3,9) problem is to arrange 3 sets of the numbers 1 to 9 so that the first two 1’s and 
  the second two 1’s appear one number apart, the first two 2’s and the second two 2’s appear 
  two numbers apart, etc.
  """
 
  Also see:
  * John E. Miller: Langford's Problem
    http://dialectrix.com/langford.html
  
  * Encyclopedia of Integer Sequences for the number of solutions for each k
    http://www.research.att.com/cgi-bin/access.cgi/as/njas/sequences/eisA.cgi?Anum=014552

  This model is (compared to http://hakank.org/picat/langford.pi) 
  a generalized version, i.e. where K >= 2.


  Note: For k=4,n=2 there are two different solutions:
     solution:[4,1,3,1,2,4,3,2]
     position:[2,5,3,1,4,8,7,6]
  and
     solution:[2,3,4,2,1,3,1,4]
     position:[5,1,2,3,7,4,6,8]

  Which this symmetry breaking

     Solution[1] #< Solution[K2],

  then just the second solution is shown.


  For n=9 k=3 there are 3 solutions (with symmetry breaking):

   [1,9,1,6,1,8,2,5,7,2,6,9,2,5,8,4,7,6,3,5,4,9,3,8,7,4,3]
   [1,9,1,2,1,8,2,4,6,2,7,9,4,5,8,6,3,4,7,5,3,9,6,8,3,5,7]
   [1,8,1,9,1,5,2,6,7,2,8,5,2,9,6,4,7,5,3,8,4,6,3,9,7,4,3]

  Some solutions:
   * n=10, k=3: 
     [1,10,1,6,1,7,9,3,5,8,6,3,10,7,5,3,9,6,8,4,5,7,2,10,4,2,9,8,2,4]

   * n=17, k=3:
     [4,17,5,12,16,4,11,15,5,2,4,10,2,13,5,2,12,14,11,17,3,16,10,15,3,9,7,13,3,12,11,8,14,10,7,9,6,17,16,15,8,13,7,6,1,9,1,14,1,8,6]

   * n=18, k=3:
     [5,18,11,6,17,2,5,16,2,13,6,2,5,7,11,14,8,6,12,15,18,7,17,13,16,8,11,10,3,7,14,12,3,9,8,15,3,13,10,18,17,16,4,9,12,14,1,4,1,10,1,15,4,9]


  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.
import cp.

main => go.

go ?=>
  N = 9,
  K = 3,
  printf("N: %d K: %d\n", N,K),
  langford(N, K, Solution),
  printf("Solution: %w\n", Solution),
  fail,
  nl.

go => true.

%
% Get the first (if any) solutions for N in 2..40
%
go2 => 
  Found = [],
  foreach(N in 2..40, K in 3..3, N > K) 
    printf("N: %d K: %d\n", N,K),
    if time2(langford(N,K, Solution)) then
      flush(stdout),
      if nonvar(Solution) then 
         printf("Solution: %w\n", Solution),
         Found := Found ++ [[k=K,n=N]],
         println(found=Found)
      else 
         printf("No solution\n")
      end,
      nl,
      flush(stdout)
    end
  end,
  println(found=Found).


% Count the number of solutions
go3 => 
  Map = get_global_map(),
  foreach(N in 2..40) 
    foreach(K in 3..4, N > K)
      % println([n=N,k=K]),
      Map.put(count,0),
      ((langford(N,K, Solution), Map.put(count, Map.get(count)+1), fail) ; true),
      printf("N: %d K: %d = %d solutions\n", N, K, Map.get(count)),
      flush(stdout)
    end,
    nl
  end.


 
langford(N, K, Solution) =>

  if K = 2, not ((N mod 4 == 0; N mod 4 == 3)) then
     printf("There is no solution for N and K=2 unless N mod 4 == 0 or N mod 4 == 3"),
     fail
  end,

  NK = N*K,
  Solution = new_list(NK),
  Solution :: 1..N,

  Positions = new_list(NK),
  Positions :: 1..NK,

  all_distinct(Positions),

  Js = [], % temporary variables
  foreach(I in 1..N)
     J :: 1..K*N - ((K-1)*I),
     Js := Js ++ [J],
     foreach(C in 0..K-1)
       J2 #= J+(I*C)+C,
       Js := Js ++ [J2],
       element(J2, Solution, I),
       Positions[(I-1)*K+C+1] #= J2
     end
  end,

  % ensure that there are N occurrences of each number
  GCC = $[I-K : I in 1..N],
  global_cardinality(Solution, GCC),

  % symmetry breaking
  Solution[1] #< Solution[NK],

  Vars = Positions ++ Js ++ Solution,
  solve([ffc,split], Vars).
