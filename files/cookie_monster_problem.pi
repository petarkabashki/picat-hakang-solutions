/* 

  Cookie Monster Problem in Picat.

  Richard Green
  "The Cookie Monster Problem"
  https://plus.google.com/u/0/101584889282878921052/posts/8qWvSaLJVGD
  """
  Suppose that we have a number of cookie jars, each containing a certain number of cookies. 
  The Cookie Monster wants to eat all the cookies, but he is required to do so in a number 
  of sequential moves. At each move, the Cookie Monster (CM) chooses a subset of the jars, 
  and eats the same (nonzero) number of cookies from each jar. The goal of the CM is to 
  empty all the cookies from the jars in the smallest possible number of moves, and the 
  Cookie Monster Problem is to determine this number for any given set of cookie jars.
  
  Since the CM has an unlimited appetite, there is essentially no difference between eating 
  three cookies from one jar, and eating three cookies from each of 10 jars. It turns out 
  that there is no advantage to depleting these 10 jars at different rates, so the 10 jars 
  may as well be one jar, and we may as well reduce to the case where no two jars contain 
  the same number of cookies. It is also safe to ignore any empty jars. This means that the 
  starting state of the problem may be described by a set of positive integers, S. 
  The Cookie Monster Number of S, CM(S), is the smallest number of moves in which this set 
  of jars can be completely emptied.

  Let's look at an example. Suppose that S is the set {15, 13, 12, 4, 2, 1}, meaning that there 
  are six jars, containing 1, 2, 4, 12, 13 and 15 cookies each. Here are three possible 
  strategies open to the CM.

  ...
  
  The recent paper http://arxiv.org/abs/1304.7508 by Megan Belzner gives a good introduction 
  to the Cookie Monster Problem. The paper looks at some special cases of the problem, 
  including the case where the set S has size 3. In this case, it takes three moves to 
  empty S, unless the sum of the cookies in two of the jars equals the number in the 
  third, in which case it only takes two moves. Some other cases examined in the paper 
  are arithmetic progressions (which sometimes achieve the lower bound), geometric 
  progressions (which sometimes achieve the upper bound) and the Fibonacci sequence 
  (which can be emptied in a number of moves roughly equal to half the number of jars). 
  The problem can also be altered to form a two-player game, similar to the game of Nim, 
  and the author discusses this to some extent.
  """

  For this problem instance there are 28 different solutions with 4 moves (optimal solution)
    z: [1, 11, 1, 2]
    z: [1, 11, 2, 1]
    z: [1, 11, 3, 1]
    z: [2, 10, 2, 1]
    z: [3, 9, 2, 1]
    z: [4, 8, 2, 1]
    z: [8, 4, 2, 1]
    z: [9, 3, 2, 1]
    z: [9, 4, 2, 1]
    z: [10, 2, 2, 1]
    z: [10, 3, 1, 1]
    z: [10, 3, 2, 1]
    z: [10, 4, 2, 1]
    z: [11, 1, 1, 2]
    z: [11, 1, 2, 1]
    z: [11, 1, 3, 1]
    z: [11, 2, 1, 1]
    z: [11, 2, 2, 1]
    z: [11, 3, 1, 1]
    z: [11, 3, 2, 1]
    z: [11, 4, 1, 1]
    z: [11, 4, 2, 1]
    z: [12, 1, 2, 1]
    z: [12, 2, 1, 1]
    z: [12, 2, 2, 1]
    z: [12, 3, 1, 1]
    z: [12, 3, 2, 1]
    z: [12, 4, 2, 1]


  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

%
% Solve the original
% 
go ?=>
  nolog,
  data(1,Jars,MaxMoves),
  do_cookie_monster(Jars,MaxMoves),
  nl.

go => true.

%
% Run the simple instances 1..3
%
go2 =>
  nolog,
  foreach(P in 1..4)
    println(problem=P),
    data(P,Jars,MaxMoves),
    time2(do_cookie_monster(Jars,MaxMoves)),
    nl
  end,
  nl.

%
% Problem 5 (hard)
% SAT: 19.4s
% CP:
%
% Solution by SAT:
%
% numMoves = 7
% cookies_picked = [33,53,16,8,2,4,1]
% jars = [100,86,59,58,56,52,48,47,41,30,27,24,23,18,17,16,14,9,3,1]
% Take 33 cookie(s): {67,53,26,25,23,19,15,14,8,30,27,24,23,18,17,16,14,9,3,1}
% Take 53 cookie(s): {14,0,26,25,23,19,15,14,8,30,27,24,23,18,17,16,14,9,3,1}
% Take 16 cookie(s): {14,0,10,9,7,3,15,14,8,14,11,8,7,2,1,0,14,9,3,1}
% Take  8 cookie(s): {6,0,2,1,7,3,7,6,0,6,3,0,7,2,1,0,6,1,3,1}
% Take  2 cookie(s): {4,0,0,1,5,1,5,4,0,4,1,0,5,0,1,0,4,1,1,1}
% Take  4 cookie(s): {0,0,0,1,1,1,1,0,0,0,1,0,1,0,1,0,0,1,1,1}
% Take  1 cookie(s): {0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0}
%
go3 =>
  nolog,
  data(5,Jars,MaxMoves),
  time2(do_cookie_monster(Jars,MaxMoves)),
  nl.

%
% Problem 6 (harder)
%
go4 =>
  nolog,
  data(6,Jars,MaxMoves),
  time2(do_cookie_monster(Jars,MaxMoves)),
  nl.


%
% Run and print solution
%
do_cookie_monster(Jars,MaxMoves) =>
  println(n=Jars.len),
  cookie_monster(Jars,MaxMoves, X,Z,NumMoves),

  println(numMoves=NumMoves),
  println(cookies_picked=[I : I in Z, I > 0]),
  println(jars=Jars),
  foreach(I in 2..NumMoves+1)
    printf("Take %2d cookie(s): %w\n", Z[I],X[I])
  end,
  
  nl.

%
% Solve the Cookie Monster problem
%
cookie_monster(Jars,MaxMoves, X,Z,NumMoves) =>

  N = Jars.len,
  MaxJars = max(Jars),

  % cookies left are eating z cookies from each jar
  X = new_array(MaxMoves,N),
  X :: 0..MaxJars,
  
  Z = new_list(MaxMoves),
  Z :: 0..MaxJars,
  
  NumMoves :: 1..MaxMoves, % number of moves needed

  foreach(I in 1..N) 
    X[1,I] #= Jars[I]
  end,
  Z[1] #= 0,

  % Check each move
  foreach(M in 2..MaxMoves) 
    foreach(I in 1..N) 
       (Z[M] #<= X[M-1,I]) #=> (X[M,I] #= X[M-1,I] - Z[M]),
       (Z[M] #> X[M-1,I]) #=> (X[M,I] #= X[M-1,I])
    end
  end,

  % handle the steps after the last move 
  MM :: 2..MaxMoves, 
  NumMoves #= MM-1,
  foreach(I in 1..N)
    matrix_element(X,MM,I,0)
  end,
  
  foreach(M2 in 1..MaxMoves)
    M2 #> MM #=> Z[M2] #= 0
  end,

  % Symmetry breaking
  % decreasing(Z[2..MaxMoves]), % slower

  Vars = X.vars ++ Z ++ [MM],
  println(solve),
  solve($[min(NumMoves),max,split,report(printf("numMoves=%d\n",NumMoves))],Vars).


%
% data
%


%
% Original problem (see above).
%
data(1,Jars,MaxMoves) =>
  Jars = [15, 13, 12, 4, 2, 1],
  MaxMoves = 5.


%
% Megan Belzner: "Emptying Sets: The Cookie Monster Problem"
% From http://arxiv.org/abs/1304.7508
% page 1
% """
% The initial formulation presents a set of n = 15 cookie jars with i cookies in 
% the ith jar. 
% """
% There is just one optimal solution to this problem: 4 steps [8,4,2,1]
%
data(2,Jars,MaxMoves) =>
  N = 15,
  Jars = [I : I in 1..N],
  MaxMoves = N+1.


data(3,Jars,MaxMoves) =>
  N = 30,
  Jars = [I : I in 1..N],
  MaxMoves = N+1.

%
% Reverse version of #3
% 30..1 is faster than 1..30
%
data(4,Jars,MaxMoves) =>
  N = 30,
  Jars = [I : I in 1..N].reverse,
  MaxMoves = N+1.

%
% Harder problem.
%
data(5,Jars,MaxMoves) =>
  Jars = [100,86,59,58,56,52,48,47,41,30,27,24,23,18,17,16,14,9,3,1],
  MaxMoves = Jars.len+1.


%
% Even harder
%
data(6,Jars,MaxMoves) => 
  Jars = [989,965,947,899,898,855,804,796,758,745,729,719,672,625,588,573,558,537,536,535,504,502,500,493,473,404,381,363,264,254,240,232,212,180,142,94,14,9,7,4],
  MaxMoves = Jars.len+1.

