/*

  Torn numbers in Picat.

  From
  http://www.comp.nus.edu.sg/~henz/projects/puzzles/digits/torn.html?19

  """
  The Torn Number from "Amusements in Mathematics, Dudeney", number 113

  I had the other day in my possession a label bearing the number 3025
  in large figures. This got accidentally torn in half, so that 30 was
  on one piece and 25 on the other. On looking at these pieces I began
  to make a calculation, scarcely concious of what I was doing, when I
  discovered this little peculiarity. If we add the 30 and the 25
  together and square the sum we get as the result the complete original
  number on the label! Now, the puzzle is to find another number,
  composed of four figures, all different, which may be divided in the
  middle and produce the same result. 
  """

  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go =>
   L = findall([LD, Sum], $torn(LD, Sum)),
   write(L),nl.


torn(LD, Sum) =>
   LD = [D3, D2, D1, D0],
   LD :: 0..9,

   all_different(LD),
   D3 #!= 0,
   Sum #= D3 * 10 + D2 + D1 * 10 + D0,
   Sum * Sum #= D3 * 1000 + D2 * 100 + D1 * 10 + D0,

   solve([], LD).
